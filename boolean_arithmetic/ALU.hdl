/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:

// if (zx==1) set x = 0        // 16-bit constant
    Mux16(a=x, b=false, sel=zx, out=x2);

// if (nx==1) set x = ~x       // bitwise "not"
    Not16(in=x2, out=negx);
    Mux16(a=x2, b=negx, sel=nx, out=xval);

// if (zy==1) set y = 0        // 16-bit constant
    Mux16(a=y, b=false, sel=zy, out=y2);

// if (ny==1) set y = ~y       // bitwise "not"
    Not16(in=y2, out=negy);
    Mux16(a=y2, b=negy, sel=ny, out=yval);

// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
    Add16(a=xval, b=yval, out=added);
    And16(a=xval, b=yval, out=anded);
    Mux16(a=anded, b=added, sel=f, out=fresult);

// if (no==1) set out = ~out   // bitwise "not"
// if (out<0) set ng = 1
    Not16(in=fresult, out=negfresult);
    Mux16(a=fresult, b=negfresult, sel=no, out=out,
        out[0..7]=lshalf,
        out[8..15]=hshalf,
        out[15]=ng);

// if (out==0) set zr = 1
    Or8Way(in=lshalf, out=lsIsZero);
    Or8Way(in=hshalf, out=hsIsZero);
    Or(a=lsIsZero, b=hsIsZero, out=negzr);
    Not(in=negzr, out=zr);

}